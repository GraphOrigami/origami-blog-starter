{
  // Define a pipeline that starts with the folder of markdown content and
  // applies a series of transformations to process them into a form that's
  // ready to be displayed on the various types of pages.
  processed = content
    // Convert the markdown to HTML, changing the extension from `md` to `html`.
    → @mapFn(@mdHtml)
    // Add a `date` field parsed from the filename.
    → @mapFn((post, fileName) => @merge(post, {
      date: parseDate.ori(fileName)
    }))
    // Add next/previous keys. The posts are already in chronological order
    // because their names start with a YYYY-MM-DD date, so we can determine the
    // next and previous posts by looking at the adjacent posts in the list. We
    // need to do this before reversing the order in the next step because we
    // want "next"/"previous" to go in chronological order.
    → @addNextPrevious
    // Finally, reverse to get posts in reverse chronological order.
    → @reverse

  // Turn all the posts into a feed.
  feed = feed.ori(processed)

  // Construct the site's tree of public resources to deploy.
  public = {
    // About page
    about.html = page.ori(@mdHtml(about.md))

    // Assets (fonts, styles) and images
    assets
    images

    // Use the first page of posts as the home page.
    index.html = pages/1.html

    // Posts grouped into pages: 1.html, 2.html, etc.
    pages =
      // Group the posts into sets of 5 posts each.
      @paginate(processed, 5)
      // Turn each set of posts into HTML.
      → @mapFn({
        extension: "→html"
        value: multiPostPage.ori
      })

    // The posts area contains a page for each individual post.
    posts = @map(processed, singlePostPage.ori)

    // Feeds
    feed.json = @json(feed)
    rss.xml = @rss(feed)
  }

  // From the above, we return the `public` tree as the program's output.
}/public
