{
  // Define the overall public site.
  public = {
    // Stylesheet and images
    assets

    // Index page lists all posts
    index.html = index.ori(@mdHtml(index.md), newestPostsFirst)

    // About page
    about.html = page.ori(@mdHtml(about.md))

    // The posts area contains one page for each post
    posts = @map(@sequence(postsByDate), postPage.ori)

    // Feeds
    feed.json = @json(feed)
    rss.xml = @rss(feed)
  }

  // HTML fragments (not complete pages) for the posts
  bodies = @map(content, @mdHtml)

  // Chronological order
  postsByDate = @sortBy(bodies, (post) => post/date)

  // Reverse chronological order
  newestPostsFirst = @reverse(postsByDate)

  // Data for feeds
  feed = feed.ori(newestPostsFirst)
}
